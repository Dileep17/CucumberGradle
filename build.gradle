apply plugin: 'java'

project.ext {
    cucumberVersion = '4.7.4'
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:' + cucumberVersion
    testImplementation 'io.cucumber:cucumber-junit:' + cucumberVersion
    testImplementation 'io.cucumber:cucumber-picocontainer:' + cucumberVersion
    testImplementation 'de.monochromata.cucumber:reporting-plugin:4.0.7'
    testImplementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
    testImplementation 'org.hamcrest:hamcrest-core:2.1'
}

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

def cucumberOptions = ['-p', 'pretty',
                       '--add-plugin', 'de.monochromata.cucumber.report.PrettyReports:build/cucumber-report/html-reports/cucumber-html',
                       '-p', 'json:build/cucumber-report/json-report/cucumber-report.json',
                       '-g', 'gradle.cucumber',
                       '-g', 'spike.cucumber.hooks',
                       '-g', 'spike.cucumber.driver',
                       '-g', 'spike.cucumber.pages',
                       '-g', 'spike.cucumber.steps',
                       'src/test/resources/spike/cucumber',]


def runParallel = project.properties['parallel'] ?: "false"
def ThreadsOption = ['--threads', Runtime.runtime.availableProcessors().intdiv(2) ?: 1 as String]

def tags = project.properties['tags'] ?: null
def TagOptions = ['--tags', tags]

task cucumber() {
    dependsOn assemble, compileTestJava
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            if (runParallel == "true") cucumberOptions.addAll(ThreadsOption)
            if (tags != null) cucumberOptions.addAll(TagOptions)
            args = cucumberOptions
        }
    }
}